---
page_title: "scylladbcloud_vpc_peering Resource - terraform-provider-scylladbcloud"
subcategory: ""
description: |-
  
---

# scylladbcloud_vpc_peering (Resource)



## Example Usage

```terraform
# Create a VPC peering connection to the specified datacenter.
resource "scylladbcloud_vpc_peering" "example" {
	cluster_id = 1337
	datacenter = "AWS_US_EAST_1"

	peer_vpc_id     = "vpc-1234"
	peer_cidr_block = "192.168.0.0/16"
	peer_region     = "us-east-1"
	peer_account_id = "123"

	allow_cql = true
}

output "scylladbcloud_vpc_peering_connection_id" {
	value = scylladbcloud_vpc_peering.example.connection_id
}
```

## Example Usage for AWS

```terraform
# End-to-end example for ScyllaDB Datacenter VPC peering on AWS.
resource "aws_vpc" "app" {
	cidr_block = "10.0.0.0/16"
}

data "aws_caller_identity" "current" {}

resource "scylladbcloud_vpc_peering" "example" {
	cluster_id = 1337
	datacenter = "AWS_EAST_1"

	peer_vpc_id     = aws_vpc.app.id
	peer_cidr_block = aws_vpc.app.cidr_block
	peer_region     = "us-east-1"
	peer_account_id = data.aws_caller_identity.current.account_id

	allow_cql = true
}

resource "aws_vpc_peering_connection_accepter" "app" {
    vpc_peering_connection_id = scylladbcloud_vpc_peering.example.connection_id
    auto_accept               = true
}

resource "aws_route_table" "bench" {
	vpc_id = aws_vpc.app.id

	route {
		cidr_block = scylladbcloud_cluster.example.cidr_block
		vpc_peering_connection_id = aws_vpc_peering_connection_accepter.app.vpc_peering_connection_id
	}
}
```

## Example Usage for GCP

```terraform
# End-to-end example for ScyllaDB Datacenter network peering on GCP.
resource "google_compute_network" "app" {
	name = "app"
	auto_create_subnetworks = true
}

resource "scylladbcloud_vpc_peering" "example" {
	cluster_id = 1337
	datacenter = "GCE_US_CENTRAL_1"

	peer_vpc_id     = google_compute_network.app.name
	peer_region     = "us-central1"
	peer_account_id = "exampleproject"

	allow_cql = true
}

resource "google_compute_network_peering" "app" {
	name         = "app-peering"
	network      = google_compute_network.app.self_link
	peer_network = scylladbcloud_vpc_peering.example.network_link
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `cluster_id` (Number) Cluster ID
- `datacenter` (String) Cluster datacenter name
- `peer_account_id` (String) Peer Account ID
- `peer_region` (String) Peer VPC region
- `peer_vpc_id` (String) Peer VPC ID

### Optional

- `allow_cql` (Boolean) Whether to allow CQL traffic
- `peer_cidr_block` (String) Peer VPC CIDR block, this attribute also supports comma separated list of CIDR blocks, but this is deprecated and will be removed in the future versions of the provider, for multiple CIDR blocks use peer_cidr_blocks attribute instead
- `peer_cidr_blocks` (List of String) Peer VPC CIDR block list
- `timeouts` (Block, Optional) (see [below for nested schema](#nestedblock--timeouts))

### Read-Only

- `connection_id` (String) VPC peering connection id
- `id` (String) The ID of this resource.
- `network_link` (String) (GCP) Cluster VPC network self_link
- `vpc_peering_id` (Number) Cluster VPC Peering ID

<a id="nestedblock--timeouts"></a>
### Nested Schema for `timeouts`

Optional:

- `create` (String)
- `delete` (String)
- `update` (String)

## Import

Import is supported using the following syntax:

```shell
# A VPC peering connection can be imported by specifying the numeric identifier.
terraform import scylladbcloud_vpc_peering.example 123
```
